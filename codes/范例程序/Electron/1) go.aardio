//快速入门
import win.ui;
/*DSG{{*/
var winform = win.form(text="在aardio中嵌入electron";right=1276;bottom=767;clipch=1)
winform.add()
/*}}*/

import electron.runtime; // 如果希望自动安装electron到系统目录,请先导入electron.runtime
import electron.app; // 导入electron扩展库
var theApp = electron.app(winform); // 创建electron进程,如果第4个参数为true，可以UTF8模式打开控制台 - 用于查看electron主进程输出

//启动主进程的 main.js
theApp.jsMain =/**  
	// 启动RPC服务允许aardio/electron互调函数,创建BrowserWindow主窗口
    const aardio = require('aardio') 
    
    // 管理electron进程的生命周期 
    const app = require('electron').app 
    
	aardio.ready( win=> { 
		if( !aardio.studioInvoke ){
			win.removeMenu()
		} 
		
		win.on('closed', () => {  
			
		})  	
	} )

	app.on('window-all-closed', () => {
		app.quit(); // 退出electron进程
	}) 
**/

//启用远程调试，并自动分配空闲不会冲突的端口
theApp.remoteDebuggingPort = 0;

//直接打开网页
theApp.start("http://bbs.aardio.com") 

winform.show() 
winform.text = "正在等待electron打开远程调试服务............."

import web.socket.chrome;
var wsRemotetDebugging = web.socket.chrome();

//等待electron打开远程调试服务
wsRemotetDebugging.waitForConnected();
 
//使用远程调试端口控制网页
wsRemotetDebugging.Page.navigate( url = "http://bbs.aardio.com/forum.php?mod=viewthread&tid=11486&from=portal")
winform.text = "已连接到electron的WebSocket调试接口: " + wsRemotetDebugging.rpc.url;

win.loopMessage();
